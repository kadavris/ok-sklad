{$I ok_sklad.inc}
unit ActivationDlg;

interface

uses
  Windows, Messages, SysUtils, Variants, Classes, Graphics, Controls, Forms,
  Dialogs, ssBaseDlg, ActnList, ssFormStorage, ImgList, xButton,
  ssSpeedButton, ssPanel, ssGradientPanel, ExtCtrls, cxLabel, cxControls,
  cxContainer, cxEdit, cxTextEdit,
  cxProgressBar, cxRadioGroup, ComCtrls;

type
  TActivationDlg = class(TBaseDlg)
    pcMain: TPageControl;
    tsLogin: TTabSheet;
    lEmail: TcxLabel;
    edEmail: TcxTextEdit;
    edPassword: TcxTextEdit;
    lPassword: TcxLabel;
    tsDistributions: TTabSheet;
    rgDistributions: TcxRadioGroup;
    tsTimer: TTabSheet;
    Timer1: TTimer;
    pbMain: TcxProgressBar;
    btnActivate: TxButton;
    tsServerAddress: TTabSheet;
    lServAddr: TcxLabel;
    edServAddr: TcxTextEdit;
    btnServaddr: TxButton;
    lServAddrMsg: TcxLabel;
    lDistrMsg: TcxLabel;
    lLoginMsg: TcxLabel;
    lTimerMsg: TcxLabel;
    btnHost: TssSpeedButton;

    procedure FormCloseQuery(Sender: TObject; var CanClose: Boolean);
    procedure FormShow(Sender: TObject);
    procedure FormCreate(Sender: TObject);
    procedure btnActivateClick(Sender: TObject);
    procedure rgDistributionsPropertiesChange(Sender: TObject);
    procedure aOKExecute(Sender: TObject);
    procedure btnServaddrClick(Sender: TObject);
    procedure btnHostClick(Sender: TObject);
    procedure Timer1Timer(Sender: TObject);

  private
    Host, URL, Req, Resp: String;
    ServerConnChecked: Boolean;
    FkeyState: Integer;

    procedure SetCaptions; override;
    procedure StartTimer(secs: Integer; Msg: String);
    procedure StopTimer;
    function  CheckWebConn: Boolean;
    function  CheckServerConnection: Boolean;

  public
    ServerHardwareID, newServerKey: String;
    AskForServerAddress: Boolean;
    function getKeyState: Integer;

  end;

var
  fActivationDlg: TActivationDlg;

//==============================================================================================
implementation

uses
  WebReq, ssRegUtils, prConst, fMessageDlg, ClientData, prFun, Protection,
  xLngDefs, SelectHost, StrUtils;

const
  resSection = 'ActDlg';

{$R *.dfm}

//==============================================================================================
function TActivationDlg.getKeyState: Integer;
begin
  Result := FkeyState;
end;

//==============================================================================================
procedure TActivationDlg.Timer1Timer(Sender: TObject);
begin
  pbMain.Position := pbMain.Position + 1;
  Application.ProcessMessages;
end;

//==============================================================================================
procedure TActivationDlg.FormCreate(Sender: TObject);
begin
  inherited;
  SetCaptions;
  
  AskForServerAddress := False;
  ServerConnChecked := True;

  Host := URL_KeyHost;
  URL := URL_KeyActivate;
end;

//==============================================================================================
procedure TActivationDlg.StartTimer(secs: Integer; Msg: String);
begin
  pbMain.Position := 0;
  pbMain.Properties.Max := secs;
  pcMain.ActivePage := tsTimer;
  lTimerMsg.Caption := rs(resSection, Msg);
  Timer1.Enabled := True;
  Application.ProcessMessages;
end;

//==============================================================================================
procedure TActivationDlg.StopTimer;
begin
  Timer1.Enabled := False;
  pbMain.Position := pbMain.Properties.Max;
  Application.ProcessMessages;
end;

//==============================================================================================
procedure TActivationDlg.SetCaptions;
begin
  inherited;

  panTitleBar.Caption := rs(resSection, 'Title');

  lEmail.Caption := rs(resSection, 'lEmail');
  lPassword.Caption := rs(resSection, 'lPassword');

  lDistrMsg.Caption := rs(resSection, 'lDistrMsg');
  lLoginMsg.Caption := rs(resSection, 'lLoginMsg');
  lTimerMsg.Caption := rs(resSection, 'lTimerMsg');

  lServAddr.Caption := rs(resSection, 'lServAddr');
  lServAddrMsg.Caption := rs(resSection, 'lServAddrMsg');

  btnHost.Hint := RS('frmLogin', 'SelectHost');
  btnActivate.Caption := RS(resSection, 'btnActivate');
  btnServAddr.Caption := RS(resSection, 'btnServAddr');
end;

//==============================================================================================
procedure TActivationDlg.rgDistributionsPropertiesChange(Sender: TObject);
begin
  inherited;
  btnOK.Enabled := True;
end;

//==============================================================================================
procedure TActivationDlg.FormCloseQuery(Sender: TObject; var CanClose: Boolean);
begin
  CanClose := True;

  if ModalResult in [mrCancel] then Exit;

  inherited;
end;

//==============================================================================================
function TActivationDlg.CheckWebConn: Boolean;
begin
  startTimer(15, 'CheckConn');

  Result := True;
  if not WebQuickPost(Host, URL, 'op=addkey', Resp, 15000) or (AnsiMidStr(Resp,1,5) <> 'READY') then begin
    StopTimer;
    Result := False;
    ssMessageDlg(rs(resSection, 'CheckConnErr', 1) + Resp, ssmtError, [ssmbOK]);
  end;

  StopTimer;
end;

//==============================================================================================
procedure TActivationDlg.FormShow(Sender: TObject);
begin
  inherited;

  if AskForServerAddress then begin // need to ask for server address and then query its key by myself
    pcMain.ActivePage := tsServerAddress;
  end
  else begin
    if not CheckWebConn then ModalResult := mrCancel;
    pcMain.ActivePage := tsLogin;
  end;

  btnOK.Enabled := False;

  if (Host = '') or (URL = '') then ModalResult := mrCancel;
end;

//==============================================================================================
procedure TActivationDlg.btnActivateClick(Sender: TObject);
  var
    sl: TStringList;
    i, ii, n: Integer;
    item: TcxRadioGroupItem;
    AservKey, AservMsg: OleVariant;
begin
  if (trim(edEmail.Text) = '') or (trim(edPassword.Text) = '') then Exit;

  Req := 'op=addkey' + crlf + 'l=' + encodeKey(edEmail.Text) + crlf + 'p=' + encodeKey(edPassword.Text)
         + crlf + 'h=' + ServerHardwareID + crlf + 'lang=' + LangName;

  if rgDistributions.Properties.Items.Count > 0 then begin
    if rgDistributions.ItemIndex <> -1 then Exit; // no distr selected

    Req := Req + crlf + 'd=' + IntTostr(rgDistributions.Properties.Items[rgDistributions.ItemIndex].Value);
  end;

  startTimer(30, 'Activating');

  if WebQuickPost(Host, URL, Req, Resp, 30000) then begin
    StopTimer;

    sl := TStringList.Create;
    sl.Text := Resp;

    if AnsiMidStr(sl[0], 1, 8) = 'BADLOGIN' then begin
      pcMain.ActivePage := tsLogin;
      ssMessageDlg(rs(resSection, 'Err_Login'), ssmtError, [ssmbOK]);
      Exit;

    end
    else if AnsiMidStr(sl[0], 1, 2) = 'OK' then begin
      if AnsiMidStr(sl[1], 1, 4) = 'DCNT' then begin // check if we have many distributions and need to chose one

        ii := StrToInt(AnsiMidStr(sl[1], 6, length(sl[1]))); // distributions count

        rgDistributions.Clear;
        rgDistributions.Properties.BeginUpdate;
        for i := 2 to ii + 1 do begin
          item := TcxRadioGroupItem(rgDistributions.Properties.Items.Add);
          item.Value := StrToInt(AnsiMidStr(sl[i], 2, pos(' ', sl[i]) - 2 ));
          item.Caption := AnsiMidStr(sl[i], pos(' ', sl[i]) + 1, length(sl[i])) + ' (';

          if AnsiMidStr(sl[i], 1, 1) = '+'
            then item.Caption := item.Caption + rs(resSection, 'ActStateOn') + ')'
            else item.Caption := item.Caption + rs(resSection, 'ActStateOff') + ')';

        end;
        rgDistributions.Properties.EndUpdate;

        pcMain.ActivePage := tsDistributions;

        Exit;
      end; // many distrs

      sl.Delete(0);
      newServerKey := sl.Text;
      sl.Destroy;

      // pushing key to the server
      AServKey := encodeKey('type=A' + crlf + decodeKey(newServerKey));
      try
        if not dmData.Sconnection.Connected then dmData.SConnection.Open;
        dmData.Sconnection.AppServer.ClientRegInfo(AServKey, AServKey, AServMsg);
        dmData.Sconnection.Close; // probably, we won't use it here anymore ;)

        FkeyState := AServMsg; // that can be read from outside

      except
        FkeyState := -1;
      end;

      //WriteToRegStr(PrRegKey, 'AType', newServerKey); //saving key

      // now checking what our server thinks about this key
      if RegKeyIsValid(FkeyState) then begin
        ssMessageDlg(rs(resSection, 'WebRegOK'), ssmtInformation, [ssmbOK]);
        ModalResult := mrOK;
        Exit;
      end
      else ssMessageDlg(rs('frmLogin', 'Err_ActDenied'), ssmtError, [ssmbOk]); // strange shit happens. maybe banned customer
      
    end // else if sl[0] = 'OK'
    else ssMessageDlg(rs(resSection, 'WebRegErr', 1) + Resp, ssmtError, [ssmbOK]); // general error. maybe at activation server side. 
  end
  else begin // general request error
    StopTimer;
    pcMain.ActivePage := tsLogin;
    WriteToRegStr(PrRegKey, 'AType', '');  //saving key
    ssMessageDlg(rs(resSection, 'WebRegErr', 1) + Resp, ssmtError, [ssmbOK]);
  end;

end;

//==============================================================================================
// OK is enabled if distributions are exists and one of them was selected for activation
procedure TActivationDlg.aOKExecute(Sender: TObject);
begin
  inherited;
  btnActivateClick(nil);
end;

//==============================================================================================
function TActivationDlg.CheckServerConnection: Boolean;
  var
    AservKey, AservMsg: OleVariant;
begin
  ServerConnChecked := False;
  ServerHardwareID := '';

  Result := CheckSocketServer(dmData.SConnection);
  if not Result then Exit;

  try
    AservKey := '';
    AservMsg := '';
    sleep(1000);
    if not dmData.Sconnection.Connected then dmData.SConnection.Open;
    dmData.SConnection.AppServer.ClientRegInfo('', AServKey, AServMsg); // getting state of server's key
    ServerHardwareID := AservKey;
    FkeyState := AServMsg;

    ServerConnChecked := True;

  except
  end;

  Result := ServerConnChecked;
end;

//==============================================================================================
procedure TActivationDlg.btnServaddrClick(Sender: TObject);
begin
  if Trim(edServAddr.Text) = '' then edServAddr.Text := 'localhost';
  dmData.SConnection.Host := Trim(edServAddr.Text);

  if not CheckServerConnection then begin
    ssMessageDlg(rs(resSection, 'Err_ServConn'), ssmtError, [ssmbOK]);
    Exit;
  end;

  if RegKeyIsValid(FkeyState) then begin
    ModalResult := mrCancel; // key is good
    Exit;
  end;

  pcMain.ActivePage := tsLogin;
end;

//==============================================================================================
procedure TActivationDlg.btnHostClick(Sender: TObject);
begin
  with TfrmSelectHost.Create(nil) do
    try
      if ShowModal = mrOk then edServaddr.Text := lvMain.Selected.Caption;

    finally
      Free;
    end;
end;

end.

